apply plugin: 'com.android.application'
apply from: "../constants.gradle"

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion project.ext.compileSdk
    buildToolsVersion "${project.ext.buildtools}"
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    defaultConfig {
        applicationId "com.chanse.games.colorphun"
        minSdkVersion project.ext.minSdk
        targetSdkVersion project.ext.targetSdk
        versionCode 1
        versionName "1.0"
        vectorDrawables.useSupportLibrary = true
        // Enable the experimental Jack build tools.
        jackOptions {
            enabled true
            additionalParameters('jack.incremental': 'true')
        }
        versionNameSuffix ' (Beta)'
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
//            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
    // The API module for 3rd party apps
    debugCompile('com.github.ChanseGames:ChanseGames-Android:1.0.0:devDebug@aar', {
        exclude group: 'com.github.ChanseGames', module: 'app'
    })
    releaseCompile('com.github.ChanseGames:ChanseGames-Android:1.0.0:prodRelease@aar', {
        exclude group: 'com.github.ChanseGames', module: 'app'
    })
}